static def isSnapshot(version) {
    return version.contains("SNAPSHOT")
}

static def getRepositoryUrl(version) {
    if (isSnapshot(version)) {
        return 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    return 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    ext {
        gsonVersion = '2.8.8'
        okhttpVersion = '4.9.2'

        ursaVersion = '1.3.0'
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    dependencies {
        implementation 'com.google.guava:guava:31.0.1-jre'
        implementation 'org.slf4j:slf4j-api:1.7.32'

        compileOnly 'com.github.spotbugs:spotbugs-annotations:4.2.0'
    }

    group = 'io.contek.invoker'
    version = '2.16.8-SNAPSHOT'

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier.set('sources')
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier.set('javadoc')
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = "invoker-${project.name}"
                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom {
                    afterEvaluate {
                        name = project.description
                        description = project.description
                    }

                    url = 'https://github.com/contek-io/io.contek.invoker'

                    scm {
                        connection = 'scm:git:git://github.com/contek-io/io.contek.invoker.git'
                        developerConnection = 'scm:git:ssh://github.com/contek-io/io.contek.invoker.git'
                        url = 'http://github.com/contek-io/io.contek.invoker/tree/master'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'contek'
                            name = 'Dev Contek'
                            email = 'dev@contek.io'
                            organization = 'Contek'
                            organizationUrl = 'https://contek.io'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url = getRepositoryUrl(version)
                credentials {
                    username = project.hasProperty('sonatypeUsername') ? sonatypeUsername : ''
                    password = project.hasProperty('sonatypePassword') ? sonatypePassword : ''
                }
            }
        }
    }

    signing {
        required { !isSnapshot(version) }
        sign publishing.publications.mavenJava
    }
}